config {
    type: "incremental",
    schema: "dataform_staging",
    tags: ["your_tag"],
    bigquery: {
        partitionBy: "event_date",
        clusterBy: ["item_category"]
    }
}

js {
  const { getIncrementalFilters } = helpers;
  const { startDate, endDate, dateFilter, allFilter } = getIncrementalFilters(false);
}

SELECT
  event_timestamp,
  DATETIME(TIMESTAMP_MICROS(event_timestamp), "Europe/Rome") AS event_timestamp_time,
  event_date,
  data_is_final,
  session_id,
  user_pseudo_id,
  page_location,
  page_referrer, 
  items.item_id,
  items.item_name,
  items.item_category,
  items.price,
  items.quantity
FROM
  ${ref(constants.GA4_TABLE)}
WHERE
  event_name = 'add_to_cart'
  ${when(incremental(), `AND ${dateFilter}`)} ${when(!incremental(), `AND ${allFilter}`)}

pre_operations {
  DECLARE
    date_checkpoint DEFAULT (
    ${
        when(incremental(),
            `select max(event_date)+1 from ${self()} where data_is_final = true`,
            `select date_trunc(current_date(), year)`
        )
    }
    )



    
    ${
        when(incremental(),
            `delete from ${self()} where event_date >= date_checkpoint`
        )
    }
}   
